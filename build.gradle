// * * * * * * * * * * * *
// Plugins
// * * * * * * * * * * * *

plugins {
    id 'groovy'
    id 'java-gradle-plugin'

    // Publishing publicly
    id 'com.gradle.plugin-publish' version '0.10.0'

    // Publishing to Artifactory
    id 'maven-publish'
}

// * * * * * * * * * * * *
// Dependencies
// * * * * * * * * * * * *

repositories {
    jcenter()
}

dependencies {
    compile     gradleApi()
    testCompile gradleTestKit()
    testCompile 'junit:junit:4+'
}

// * * * * * * * * * * * *
// Project configuration
// * * * * * * * * * * * *

project.description     = 'Logger Generator Plugin for Logger Generator tooling as described in https://looking4q.blogspot.com/2018/11/logging-cutting-edge-practices.html'
project.group           = 'com.navid.loggergenerator'
project.version         = '1.2.0'


project.ext.name        = 'LoggerGeneratorPlugin'
project.ext.pluginId    = 'com.navid.LoggerGeneratorPlugin'
project.ext.scm         = 'scm:git@github.com/albertonavarro/loggergenerator-gradle-plugin.git'
project.ext.url         = 'https://github.com/albertonavarro/loggergenerator-gradle-plugin'

// Configuration for: java-gradle-plugin
gradlePlugin {
    plugins {
        LoggerGeneratorPlugin {
            id = project.pluginId
            implementationClass = 'com.navid.loggergenerator.plugin.LoggerGeneratorPlugin'
        }
    }
}

// * * * * * * * * * * * *
// Publishing
// * * * * * * * * * * * *

// Consult the accompanied blog post for information on publishing the plugin.
// A link can be found in the README file.

// Publishing to plugins.gradle.org
// --------------------------------

// Configuration for: com.gradle.plugin-publish
pluginBundle {
    website = project.url
    vcsUrl = project.scm
    tags = ['loggergenerator', 'looking4q', 'navid']

    plugins {
        LoggerGeneratorPlugin {
            displayName = 'Logger Generator Plugin'
            id          = project.pluginId
            description = project.description
        }
    }
}

// Publishing to Artifactory
// -------------------------

// Configures the publication to push, and the repository to push to
publishing {
    publications {
        pluginPublication (MavenPublication) {
            from        components.java
            groupId     project.group
            artifactId  project.name
            version     project.version
        }
    }
    repositories {
      /*  maven {
            url project.rtfUrl
            credentials {
                username project.rtfUser
                password project.rtfSecret
            }
        }
   */ }
}
